/*
  This is a textX specification of pyFlies DSL for cognitive test
  experiments definition.
  Author: Igor R. Dejanovic <igor DOT dejanovic AT gmail DOT com>
  Copyright: (c) 2014-2020 Igor R. Dejanovic <igor DOT dejanovic AT gmail DOT com>
  License: GPLv3 License
*/

import common
import components

PyFliesModel:
  (description=Description)?
  vars*=VariableAssignment
  routines+=Routine
  (flow=Flow)?
  targets*=Target
;

Routine:
  Test | Screen
;

VariableAssignment:
    name=VariableName '=' value=Expression
;

TimeReference: start_relative?='.' (relative_op=SignOperator)? time=AdditiveExpression;

/*
    Test specification
*/

Test:
  "test" name=ID "{"
    'conditions' '{'
        table=ConditionsTable
    '}'

    'trial' '{'
        vars*=VariableAssignment
        cond_components+=ConditionsComponents
    '}'
  "}"
;

ConditionsTable:
    // Condition table is given in orgmode table format. Various editor plugins
    // exists to support convenient editing of this table format.

    // Variable names are in the first line of condition specification
    '|' variables+=WORD['|'] '|'
    /\|(-*\+)+-*\|/

    // The rest of the description are condition specifications, one per line
    // The order of condition values match the param name positions.
    cond_specs+=Condition
;

Condition:
  '|' var_exps+=Expression['|'] '|'
;

ConditionsComponents:
  // Condition components is given in the form of
  // condition match expression : components definitions
  condition=Expression ':' comp_times+=ComponentTime
;


/*
    Components spec
*/

ComponentTime:
    ('at' at=TimeReference)? component=Component ('for' duration=AdditiveExpression)?;

Component: type=[ComponentType|ID|+m:comp_types] '(' params*=ComponentParam[',']  ')';
ComponentParam: type=[ParamType|ID|+m:..~type.~extends*.param_types] value=Expression;

WORD:
    INT|/[-\w]*\b/
;


Statement:
    Repeat | Show
;

Repeat:
  'repeat' what=Repeatable runs=INT ('times' | 'time')?
    full_random?=FullRandom random?='random' ('as'? practice?='practice')?
;

Repeatable:
    Block | TestRef
;

TestRef: test=[Test];

Block:
    '{'
        statements*=Statement
    '}'
;

FullRandom: 'full' 'random';

Show:
  'show' screen = [Screen]
;

Flow:
  'flow' '{'
    statements *= Statement
  '}'
;

Screen:
  'screen' name=ID "{"
  /*    content=/(.|\n)*?(?=})/  */
      content=/[^}]*/
  '}'
;

Target:
  'target' name=ID '{'
    settings*=Setting
  '}'
;

Setting:
  name=ID '=' value=BASETYPE
;

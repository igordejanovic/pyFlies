"""
Eriksen flanker test.
"""

directions = [left, right]
congruencies = [incongruent, congruent]

test EriksenFlanker {
	conditions {
      | direction       | category          | response  |
      |-----------------+-------------------+-----------|
      | directions loop | congruencies loop | direction |
    }
	
	trial {
		fix -> cross() for 1000..3000 choose
        exec -> image(file "images/{direction}-{category}.png")
                key(valid response)
		error -> sound(freq 300) for 500
		correct -> sound(freq 700) for 300
	}
}

screen intro {
    Welcome
    -------

    In the following test you will be presented with a line of 5 arrow pointing
    left or right. You should respond by the direction of the *middle arrow* as
    fast as possible by pressing left or right arrow on the keyboard or
    touching/clicking on appropriate button on the screen.

    You will first do a practice run with 4 trials. After that a real run with
    20 trial will be performed.

    Press SPACE or touch/click the screen to continue
}

screen real {
    Real block
    ----------
    
    Now a real block of trials will be performed.

    Press SPACE or touch/click the screen to continue
}



flow {

  // Show introduction
  show intro

  // Perform practice test serie
  repeat EriksenFlanker 1 time full random as practice

  show real 
  
  // Perform real test serie (collects data)
  repeat EriksenFlanker 5 times full random

}

target PsychoPy {
  left = left
  right = right
}

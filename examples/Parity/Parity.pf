"""
A parity judgment task to assess the SNARC effect.

See e.g.:
Gevers, W., Reynvoet, B., & Fias, W. (2003). The mental representation of
ordinal sequences is spatially organized. Cognition, 87(3), B87-95.
"""

numbers = 1..9
parities = [odd, even]

test Parity {
     conditions {
        | number       | parity   | response |
        |--------------+----------+----------|
        | numbers loop | parities | parities |
     }

     stimuli {
             fix: cross() for 1000..3000 choose
             exec: text(text number)
             correct: sound(freq 500) for 300
             error: sound(freq 1000) for 300
     }
}

screen Intro {
	Parity classification
	---------------------
	
	This is a practice block.
	
	You will be presented with a digit.
	Press LEFT for odd and RIGHT for even digit.
	
	Press ENTER key for the start.
}

screen Real {
	Parity classification
	---------------------
	
	Now a REAL testing will be performed.
	
	You will be presented with a digit.
	Press LEFT for odd and RIGHT for even digit.
	
	Press ENTER key for the start.
}

structure {
	screen Intro
	test Parity 1 practice randomize
	screen Real
	test Parity 2 randomize all
}

/*
target Expyriment {
  output = "/home/igor/tmp/Parity/"
  responses {
  	// see expyriment/misc/constants.py
    odd = K_LEFT
    even = K_RIGHT
    next = K_RETURN
  }
}
*/

target PsychoPy {
  output = "/home/igor/tmp/ParityPsycho/"
  responses {
    odd = left
    even = right
  }
}

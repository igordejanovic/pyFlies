"""
A simple behavioural task to assess a Simon effect.

See also:
http://en.wikipedia.org/wiki/Simon_effect
"""

positions = [left, right]
colors = [green, red]
responses = positions

test Simon {
    conditions {
      | position       | color       | congruency                                         | response                          |
      |----------------+-------------+----------------------------------------------------+-----------------------------------|
      | positions loop | colors loop | congruent if position == response else incongruent | left if color == green else right |
    }

    /* Since this is a relatively small condition table we could just
       spell it out like:

      | position | color | congruency  | response |
      |----------+-------+-------------+----------|
      | left     | green | congruent   | left     |
      | left     | red   | incongruent | right    |
      | right    | green | incongruent | left     |
      | right    | red   | congruent   | right    |

      instead of using expressions in table cells.
    */

    stimuli{
        fix: cross() for 2000..4000 choose
        exec: rectangle(posx position, color color, fillColor color)
        error: sound(freq 1000) for 300
        correct: sound(freq 500) for 300
    }
}

screen Practice {
    Simon test
    ----------
    
    You will be presented with a colored rectangle positioned
    left or right.
    Press LEFT for the GREEN rectangle and right for the red.
    
    Press SPACE for the practice block.
}

screen Real {
    Simon test
    ----------
    
    Now a REAL testing will be performed.
    
    Press SPACE for the real block.
}

flow {
    show Practice
    repeat Simon 1 time random as practice
    show Real
    repeat Simon 10 times full random
}

target PsychoPy {
  output = "~/SimonPsycho/"
  responses {
    left = left
    right = right
  }
}
